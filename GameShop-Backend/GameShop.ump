namespace ca.mcgill.ecse321.GameShop.model;

class Account {
  abstract;
  immutable String email; 
  String password;
  lazy String name;
  lazy String phoneNumber;
}

class CustomerAccount {
  isA Account;
  Integer customerId;
  1 cardOwner <@>- * PaymentDetails paymentCards;
}

class StaffAccount {
  abstract;
  isA Account;
  Integer staffId;
}

class EmployeeAccount {
  isA StaffAccount;
  1 employee <@>- * ActivityLog logs;
  Boolean isActive;
}

class ManagerAccount {
  isA StaffAccount;
}

class ActivityLog {
  Integer logId;
  String content;
}

class RequestNote {
  Integer noteId;
  String content;
  Date noteDate;
}

class PaymentDetails {
  Integer paymentDetailsId;
  String cardName;
  String postalCode;
  Integer cardNumber;
  Integer expMonth;
  Integer expYear;
}

class CustomerOrder {
  Integer orderId;
  enum OrderStatus { SHIPPING, DELIVERED, RETURNED }
  lazy OrderStatus orderStatus;
  Date orderDate;
}

class Game {
  isA GameEntity; 
  Integer quantityInStock;
  Boolean isAvailable;
  Double price; 
}

class GameRequest {
  isA GameEntity; 
  1 gameRequest <@>- * RequestNote associatedNotes;
  
  enum RequestStatus { SUBMITTED, APPROVED, REFUSED }
  lazy RequestStatus requestStatus;
  Date requestDate;
}

class GameEntity {
  abstract;
  Integer gameEntityId;
  String name;
  String description;
  String imageURL;
}

class GameCategory {
  Integer categoryId;
  enum CategoryType { GENRE, CONSOLE }
  lazy CategoryType categoryType;
  Boolean isAvailable;
  String name;
}

class Promotion {
  Integer promotionId;
  enum PromotionType { GAME, CATEGORY }
  lazy PromotionType promotionType;
  Double discountPercentageValue;
}

class StoreInformation {
  Integer storeInfoId;
  1 info <@>- * Promotion currentPromotions; 
  lazy String storePolicy; 
}

class Review {
  Integer reviewId;
  1 reviewRecord <@>- * Reply reviewReplies; 

  enum GameReviewRating { ONE_STAR, TWO_STARS, THREE_STARS, FOUR_STARS, FIVE_STARS }
  lazy GameReviewRating rating;
  lazy String comment;
  Date reviewDate;
}

class Reply {
  Integer replyId;
  String content;
  Date replyDate;
}


class OrderGame {

}

association {
  1 OrderGame reviewedGame -- 0..1 Review review;
  1 CustomerOrder customerOrder -- 1..* OrderGame orderedGames;
  * OrderGame orders -- 1 Game game;
  1 CustomerAccount orderedBy -- * CustomerOrder orderHistory;
  * CustomerAccount wishLists -- * Game wishListedGames;
  1 PaymentDetails paymentInformation -- * CustomerOrder paidOrders;
  * Promotion promotions -- * Game promotedGames;
  * Promotion promotions -- * GameCategory promotedCategories;
  * GameEntity games -- 1..* GameCategory categories;
  1 EmployeeAccount requestPlacer -- * GameRequest requests; 
  1 ManagerAccount reviewer -- * Reply reviewReplies; 
  1 StaffAccount notesWriter -- * RequestNote writtenNotes; 
}

